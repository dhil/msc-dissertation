\section{Problem analysis}\label{sec:problem-analysis}
%Used car-dealers got a notorious reputation for being dishonest. Dishonest, because, they are reluctant to disclose any unfortunate effects that their cars may have. On the other hand, if they \emph{did} disclose all the effects then we probably would not buy them anyway as it appears too much effort to handle those effects.

Programming languages vary greatly in their approach to effects.
Some languages do not disclose potential runtime effects, e.g. the ML-family of languages. For example consider the signature \code{readFile : string $\to$ [string]} for a function in SML, its suggestive name hints that given a file name the function reads the file and return the contents line by line. In order to read a file the function must inevitably perform a side-effecting action, namely, accessing some storage medium. But this information is not conveyed in the function signature.

Other languages disclose effects, albeit with varying degree. For example Java requires programmers to annotate method signatures with potential unhandled checked exceptions that may may be raised during runtime, e.g. the signature above may be written \code{String[] readFile(String f) throws IOException} in Java. But programmers can circumvent this requirement by raising unchecked exceptions, which appears to defeat the purpose of the effect system. Moreover, due to Java's inheritance and subtyping it is a code breaking change to extend an interface with an additional effect annotation. Thus, often, programmers find it easier to avoid the effect system altogether \cite{Venners03}.

The Haskell programming language is also explicit about effects, but, in contrast to Java, it offers no escape hatch to be implicit. Haskell insists that every effectful computation is encapsulated inside an appropriate monad.\footnote{Strictly speaking it is not true as any function can be defined in terms of side-effecting \code{error} function without being reflected in the type signature.} In Haskell the above signature could be written as \code{readFile :: String $\to$ IO [String]}, where the \type{IO}-annotation signifies that the function might perform an input/output side-effect. We can think of \type{IO} as an effect type. In fact, Wadler and Thiemann gave the theoretical foundation for interpreting any monad as an effect type \cite{Wadler2003}. Section \ref{sec:problem-with-monads} continues the discussion about monads as effects. %A consequence of enforcing effectful computations to be wrapped inside a monad is that the function signature conveys additional information about the computation which the programmer and compiler can rely on. 
%However monads are not flawless.

\subsection{Benefits of being explicit about effects}
An effect is a static description of the possible state-changing actions that a computation might perform. Types and effects are complementary, together they characterise computations. A type determines the possible outputs of a computation and an effect conveys information about what might happen during evaluation. This information may be exploited by an optimising compiler to transform a computation into an equivalent, more efficient computation. For example, fine-grained effects can tell us precisely when it is safe to reuse a particular piece of code \cite{Kammar2012}, run it parallel, etc. Moreover, effects endow additional safety as they can aid in verification of programs up-front \cite{Brady2013}.

Finally, explicit effects provide additional documentation to the programmer about the code. As a result the programmer gain better insight into what the computation actually \emph{does} without breaking the abstraction.

\subsection{A monadic effectful coffee dispenser}\label{sec:problem-with-monads}
Monads are powerful abstractions for structuring computations. In particular, monads have a simple interface. For instance in Haskell programmers often only need to concern themselves with the following two monadic operators:
\begin{itemize}
  \item Bind operator (\code{>>=}), whose type is $m \, a \to (a \to m\, b) \to m \, b$, takes a monadic value of type $m \, a$ and a function, which is applied to the value inside the monad to yield a monadic value of type $m \, b$.
  \item Pure operator (\code{return}), which has type $a \to m \, a$, lifts a pure value of type $a$ into a monadic value of type $m \, a$.
\end{itemize}
Monads combine using the bind operator, which works well as long as we are working inside the same monad. 
Because, sadly, monads do not compose well \cite{Kammar2013}, and consequently it is difficult to give a monadic description of computations that might perform multiple effects.
Consider the following attempt at modelling a coffee dispenser in Haskell:
\begin{example}[Coffee dispenser using monads]\label{ex:coffee1}
The coffee dispenser is effectful, that is, it reacts to user input and may fail. Furthermore, we want to be explicit about the effects that the dispenser may perform. 

First we define the sum type \type{Dispensable} with two labels \type{Coffee} and \type{Tea} which represents dispensable drinks:
\begin{lstlisting}[style={haskell}]
data Dispensable = Coffee | Tea deriving Show

type ItemCode  = Integer
type Inventory = [(ItemCode,Dispensable)]
inventory = [(1,Coffee),(2,Tea)]
\end{lstlisting}
The \type{ItemCode} type models a button on the coffee machine, and \type{Inventory} associates buttons with dispensable items. The \code{inventory} will not change during runtime. We can capture this property in the effect signature by encapsulating the \code{inventory} inside a \type{Reader}-monad. Furthermore, we use the \type{Maybe}-type to capture the possibility of failure, e.g.
\begin{lstlisting}[style={haskell}]
dispenser :: ItemCode -> Reader Inventory (Maybe Dispensable)
dispenser n = do inv <- ask
                 let item = lookup n inv
                 return item
\end{lstlisting}
The type \type{Reader Inventory (Maybe Dispensable)} tells us that \code{dispenser} accesses a read-only instance of \type{Inventory} and maybe returns an instance of \type{Dispensable}. The \type{Maybe}-type tell us that in the event of an error we get \code{Nothing}, for instance if the user requests an item that is not in the inventory, otherwise we get \code{Just} the requested item.
The monadic operation \code{ask} retrieves the inventory from inside the \type{Reader}-monad and \code{lookup} checks whether the item \code{n} is in the inventory.  
\end{example}
Although, \code{Maybe} is a monad we cannot use its monadic interface, because we are in the context of the \code{Reader}-monad. For this simple computation it is not an issue, but it would be desirable to be able to use the failure handling capabilities of the \code{Maybe}-monad. Ideally, we would want to be able to write
\begin{lstlisting}[style={haskell}]
do inv  <- ask
   item <- lookup n inv
   return item
\end{lstlisting}
But using regular monads it is not possible to construct this type. To see why, let us desugar the above expression:
\begin{figure*}[h]
\centering
\begin{subfigure}[c]{0.40\linewidth}
\begin{lstlisting}[style={haskell}]
do inv  <- ask
   item <- lookup n inv
   return item
\end{lstlisting}
\end{subfigure}
~
\begin{subfigure}[c]{0.1\linewidth}
$\Rightarrow$
\end{subfigure}
~
\begin{subfigure}[c]{0.40\linewidth}
\centering
\begin{lstlisting}[style={haskell}]
ask >>= \inv -> 
  lookup n inv >>= \item ->
      return item
\end{lstlisting}
\end{subfigure}
\end{figure*}

The bind operator (\code{>>=}) is the problem. Recall its type
\[ \type{Monad}\; m \Rightarrow m\, a \to (a \to m\, b) \to m\, b \]
Essentially, this type tells us that we cannot compose monads of different types as the monad type $m$ is fixed throughout the computation. Consequently, it is not immediately clear how we may extend the coffee dispenser model with additional capabilities such as logging.

Suppose we want log when tea or coffee is being dispensed. The \type{Writer}-monad provide such capabilities. Ideally, we would want a monadic computation like:
\begin{lstlisting}[style={haskell}]
do inv  <- ask
   item <- lookup n inv
   tell (show item)
   return item
\end{lstlisting}
Here the monadic operation \code{tell} writes to the medium contained in the \type{Writer}-monad. However as noted above we cannot achieve this using regular monads. Basically, we want a monad whose type is something like
\[ \type{Writer w $\square$ Reader e $\square$ Maybe Dispensable} \]
where \type{w} is the type of the writable medium, \type{e} is the type of an environment and $\square$ is some ``type-glue'' that joins the types together. This type is an instance of Monad Transformer type which we will discuss in Section \ref{sec:mt}. 

\subsubsection{Effect granularity}%The IO monad is like a calzone pizza.
It is possible to solve the problem using regular monads. However, it comes at a cost as suggested by the type signature of the bind operator we can compose one monad with another as long as they got the same monadic type. So, we could just use one monadic type to describe all effects. It is very tempting to bake everything into an \type{IO}-monad as we possibly want to I/O capabilities at some point. Albeit, \type{IO} is a very conservative estimate on which effects our computation might perform. Consequently, we obtain coarse-grained effect signatures as opposed to more specific, fine-grained effect signatures.

\subsection{A better monadic effectful coffee dispenser}\label{sec:mt}
Monad Transformers enable us to combine two different monads by stacking one on top of the other.
In particular, any Monad Transformer is itself a monad, and hence we can construct arbitrarily complex compositions.
Incidentally, we can use Monad Transformers to describe computations that may perform several different effects.
The following example rewrites the coffee dispenser model from Example \ref{ex:coffee1} using Monad Transformers.
\begin{example}[Coffee dispenser using Monad Transformers]\label{ex:coffee2}
Most monads have a Monad Transformer cousin; by convention Monad Transformers have a capital T suffix, e.g. the \type{Reader}-monad's transformer is named \type{ReaderT}.

We rewrite Example \ref{ex:coffee1} to use the \type{WriterT}, and \type{ReaderT} monad instead of \type{Reader}:
\begin{lstlisting}[style={haskell}]
dispenser1 
:: ItemCode -> 
   WriterT String (ReaderT Inventory Maybe) Dispensable

dispenser1 n = do inv  <- lift ask
                  item <- lift . lift $$ lookup n inv
                  tell (show item)
                  return item
\end{lstlisting}
The type may look dubious. Basically, we have built a Monad Transformer stack with three monads:
\begin{itemize}
  \item Top of the stack: \type{WriterT} with a writable medium of type \type{String}.
  \item Middle: \type{ReaderT} with read access to an environment of type \type{Inventory}.
  \item Bottom: \type{Maybe} provides exception handling capabilities.
\end{itemize}
Monad Transformers allow us to express something reminiscent of the ``ideal'' monadic computation that we sought after in Section \ref{sec:problem-with-monads}. It is worth noting that, now,  \type{Maybe} is employed as a monad as opposed to a ordinary type. The benefits are obvious as we get the error handling capabilities of \type{Maybe} for ``free''.

However, it is not entirely free as we have to introduce \code{lift} operations. The \code{lift} operations are necessary in order to work with a specific effect down the transformer stack. For example in order to use \code{ask} we have to \code{lift} once as the \type{ReaderT} is the second type in the stack. Moreover, to use the monadic capabilities of \type{Maybe} we have to \code{lift} twice because it is at the bottom of the stack. Using \code{tell} requires no \code{lift}s in this example as \type{WriterT} is the top type. 
Consider what happens when we add yet another monad to the stack:
\begin{lstlisting}[style={haskell}]
dispenser2 
:: ItemCode -> 
   RandT StdGen (WriterT String (ReaderT Inventory Maybe)) Dispensable

dispenser2 n 
  = do r    <- getRandomR (1,20)
       inv  <- lift . lift $$ ask
       item <- lift . lift . lift $$ lookup' r n inv
       lift . tell . show $$ item
       return item
       where     
         lookup' r n inv = if r > 10 
                           then lookup n inv 
                           else Nothing
\end{lstlisting}
Here we extended our model with randomness to capture the possiblilty of failure caused by the system rather than the user. The \type{RandT} monad provides random capabilities. Moreover, we added it to the top of the transformer stack. Accordingly, we now have to use an additional \code{lift} operation everywhere, in particular, we have to \code{lift} in order to use \code{tell} now.
\end{example}
Example \ref{ex:coffee2} demonstrates that we can compose monads at the cost of lifting. The \code{lift} operations are additional boilerplate code that become necessary, because the transformer stack enforce a static ordering on effects and interactions between effect layers \cite{Kiselyov2013}.

Furthermore, the ordering leaks into the type signature which complicates modularity. For example, we may have a function which takes as input an effectful computation with type signature, say, \type{WriterT w Reader e a}.
Now, the actual effectful computation has to have a type signature with the \emph{exact} same ordering of effects even though \type{Writer} and \type{Reader} commute, i.e. the following types are isomorphic:
\[ \type{WriterT w Reader e a} \simeq \type{ReaderT e Writer w a}. \]
So, we would have to permute the type signature of the actual computation \cite{Brady2013}, e.g.
\begin{lstlisting}[style=haskell]
permute :: ReaderT e Writer w a -> WriterT w Reader e a
\end{lstlisting}
In this case it is safe because the two monads commute. But in general monads do not commute and therefore the consequence of permuting monads can be severe as we shall see in the next section.
%However Monad Transformers are no silver bullets as they impose an ordering on effects.

\subsubsection{The importance of effect ordering}
The effect ordering hard wires the semantics and syntactical structure of computations. Consider the following example adapted from O'Sullivan et. al \cite{O'Sullivan2008}:
\begin{example}[Importance of effect ordering \cite{O'Sullivan2008}]
We will demonstrate that the \type{Writer} and \type{Maybe} monads do not commute. Let \type{A} be the type 
\type{WriterT String Maybe} and \type{B} be the type \type{MaybeT (Writer String)}. The two types differ in their ordering of effects; type \type{A} has \type{Writer} as its outermost effect, whilst \type{B} has \type{Maybe} as its outermost effect. Now consider the following small program that performs one \code{tell} operation and then fails:
\begin{lstlisting}[style=haskell]
problem :: MonadWriter String m => m ()
problem = do
  tell "this is where I fail"
  fail "oops"
\end{lstlisting}
We have two possible concrete type instantiations of \type{m}, namely, either \type{A} or \type{B}. But as we shall see the two types enforce different semantics:
\begin{lstlisting}[style=haskell]
ghci> runWriterT (problem :: A ())
Nothing
ghci> runWriterT $$ runMaybeT (problem :: B ())
(Nothing, "this is where I fail")
\end{lstlisting}
When using type \type{A} we lose the result from the \code{tell} operation. Type \type{B} preserves the result.
Hence the two monads do not commute, and as a result the ordering of effects determine the semantics of the computation.
\end{example}
We have seen that, while we gain monad compositionality with Monad Transformers, we lose modularity.